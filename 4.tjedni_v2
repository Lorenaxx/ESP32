#include "Seeed_SHT35.h"
#include <U8g2lib.h>
#include <Wire.h>
#include <Dps3xx.h>
#include <Preferences.h>
#include <cmath>
// Filesystem related headers:
#include "FS.h"
#include "SPIFFS.h"
#define FORMAT_SPIFFS_IF_FAILED false // set true on new flash, one-time only

Preferences prefs;

// SHT35 sensor setup:
const unsigned char SCLPIN = 22;
const unsigned char SHT35_IIC_ADDR = 0x45;
SHT35 sensor(SCLPIN, SHT35_IIC_ADDR);

// DPS310 sensor setup (using Dps3xx library):
Dps3xx dps310;

const char* LOGFILE = "/sensor_log.csv";
// Log every 60th sample:
int sample_count = 0;
const int LOG_EVERY = 60;

// OLED setup:
U8G2_SSD1306_128X64_NONAME_F_HW_I2C u8g2(U8G2_R0);

// Pin for touch sensor
int senzorDodira = 4; // Pin za senzor dodira
int vrijednost = 0; // Varijabla za pohranu vrijednosti o훾itane sa senzora

// Variables to track the sensor readings and statistics
float temp_min = 1000.0, temp_max = -1000.0, temp_sum = 0.0;
int temp_count = 0;
float humidity_min = 100.0, humidity_max = 0.0, humidity_sum = 0.0;
int humidity_count = 0;
float pressure_min = 1000.0, pressure_max = 0.0, pressure_sum = 0.0;
int pressure_count = 0;

// Time tracking variables
unsigned long touchStartTime = 0;
bool isTouching = false;

// States for the display
enum State {
  CURRENT_VALUES,
  TEMP_STATS,
  HUMIDITY_STATS,
  PRESSURE_STATS
};

State currentState = CURRENT_VALUES;

// Structure to hold alarm thresholds
struct Alarms {
    float humidity_low = 10;
    float humidity_high = 90;
    float temperature_low = 2;
    float temperature_high = 33;
};
Alarms alarms;

// Function to calculate min, max, and average
void updateStats(float value, float &minVal, float &maxVal, float &sum, int &count) {
  if (value < minVal) minVal = value;
  if (value > maxVal) maxVal = value;
  sum += value;
  count++;
}

// Function to reset all statistics
void resetStats() {
  temp_min = 1000.0; temp_max = -1000.0; temp_sum = 0.0; temp_count = 0;
  humidity_min = 100.0; humidity_max = 0.0; humidity_sum = 0.0; humidity_count = 0;
  pressure_min = 1000.0; pressure_max = 0.0; pressure_sum = 0.0; pressure_count = 0;
}

// Function to handle touch sensor input
void handleTouch() {
  // O훾itavanje vrijednosti touch senzora
  vrijednost = touchRead(senzorDodira);
  Serial.println(vrijednost); // Provjera o훾itanja u Serial Monitoru

  // Ako je vrijednost dodira ispod praga, senzor je dodirnut
  if (vrijednost < 40) {  // Ovdje postavite prag za detekciju dodira
    if (!isTouching) {
      touchStartTime = millis();
      isTouching = true;
    } else {
      unsigned long touchDuration = millis() - touchStartTime;
      if (touchDuration >= 5000) {
        // Reset stats after 5 seconds
        resetStats();
        currentState = CURRENT_VALUES; // Show current values after reset
      } else if (touchDuration >= 2000) {
        // 2 seconds touch: Switch between stats
        switch (currentState) {
          case CURRENT_VALUES:
            currentState = TEMP_STATS;
            break;
          case TEMP_STATS:
            currentState = HUMIDITY_STATS;
            break;
          case HUMIDITY_STATS:
            currentState = PRESSURE_STATS;
            break;
          case PRESSURE_STATS:
            currentState = CURRENT_VALUES;
            break;
        }
      }
    }
  } else {
    isTouching = false;
  }
}

void appendFile(fs::FS &fs, const char * path, const char * message) {
  File file = fs.open(path, FILE_APPEND);
  if (!file) {
    Serial.print("File ");
    Serial.print(path);
    Serial.println(" not found. Attempting to create new file.");
    file = fs.open(path, FILE_WRITE);
    if (!file) {
      Serial.print("Unable to create file ");
      Serial.println(path);
      return;
    }
    Serial.println("File created.");
  }
  if (!file.print(message)) {
    Serial.print("Failed to append to file ");
    Serial.println(path);
   }
  file.close();
}

void displayFile(fs::FS &fs, U8G2& disp, const char * path) {
  Serial.printf("Reading file: %s\r\n", path);

  File file = fs.open(path);
  if (!file || file.isDirectory()) {
    Serial.println("Failed to open file for reading!");
    return;
  }
  String buffer;
  while (file.available()) {
    char next = file.read();
    buffer += next;
    if (next == '\n'){
      Serial.print(buffer);
      disp.clearBuffer();
      disp.setFont(u8g2_font_profont12_tr);
      disp.drawStr(0, 10, "Log file:");
      disp.drawStr(0, 28, buffer.c_str());
      disp.sendBuffer();
      buffer = "";
    }
  }
  file.close();
}

void setup() {
  Wire.begin();
  Serial.begin(115200);
  u8g2.begin();
  u8g2.setFont(u8g2_font_ncenB08_tr);

  prefs.begin("Veleri-OI-meteo", false);
  bool hasPrefs = prefs.getBool("valid", false);

  if (sensor.init())
    Serial.println("Sensor init failed!");
  if (!SPIFFS.begin(FORMAT_SPIFFS_IF_FAILED)) {
    Serial.println("SPIFFS Mount Failed");
    return;
  }
  delay(1000);
  displayFile(SPIFFS, u8g2, LOGFILE);

  if (sensor.init()) {
    Serial.println("SHT35 Sensor init failed!");
  } else {
    Serial.println("SHT35 Sensor init successful.");
  }

  dps310.begin(Wire);
  Serial.println("DPS310 Sensor initialized.");

  Serial.println("I2C Scan beginning.");
  for (byte address = 1; address < 128; address++) {
    Wire.beginTransmission(address);
    if (Wire.endTransmission() == 0) {
      Serial.print("I2C device found at address 0x");
      Serial.println(address, HEX);
    }
  }
  Serial.println("I2C Scan done.");
  delay(1000);

  if (hasPrefs) {
    Serial.println("Saved settings found.");
    float tmp;
    tmp = prefs.getFloat("humidity_low");
    if (!isnan(tmp)) alarms.humidity_low = tmp;
    tmp = prefs.getFloat("humidity_high");
    if (!isnan(tmp)) alarms.humidity_high = tmp;
    tmp = prefs.getFloat("temperature_low");
    if (!isnan(tmp)) alarms.temperature_low = tmp;
    tmp = prefs.getFloat("temperature_high");
    if (!isnan(tmp)) alarms.temperature_high = tmp;
  } else {
    Serial.println("Saved settings not found, using default values.");
    prefs.putFloat("humidity_low", alarms.humidity_low);
    prefs.putFloat("humidity_high", alarms.humidity_high);
    prefs.putFloat("temperature_low", alarms.temperature_low);
    prefs.putFloat("temperature_high", alarms.temperature_high);
    prefs.putBool("valid", true);
  }
  Serial.println("Configured alarms: ");
  Serial.print("Min. temp.:");
  Serial.println(alarms.temperature_low);
  Serial.print("Max. temp.:");
  Serial.println(alarms.temperature_high);
  Serial.print("Min. humidity.:");
  Serial.println(alarms.humidity_low);
  Serial.print("Max. humidity.:");
  Serial.println(alarms.humidity_high);
}

void loop() {
  float temp, hum, pressure;

  if (NO_ERROR == sensor.read_meas_data_single_shot(
        HIGH_REP_WITH_STRCH,
        &temp,
        &hum)
     ) {
    sample_count++;
    u8g2.clearBuffer();
    u8g2.setFont(u8g2_font_profont12_tr);
    String msg = "Temperature: " + String(temp) + " 째C ";
    u8g2.drawStr(0, 10, msg.c_str());
    Serial.println(msg);
    msg = "Humidity: " + String(hum) + " % ";
    u8g2.drawStr(0, 28, msg.c_str());
    Serial.println(msg);
    u8g2.sendBuffer();
    if (sample_count == LOG_EVERY) {
      msg = String(temp) + ", " + String(hum) + "\r\n";
      appendFile(SPIFFS, LOGFILE, msg.c_str());
      sample_count = 0;
    }
  } else {
    Serial.println("Sensor read failed!");
    Serial.println("");
  }

  if (Serial.available()) {
    String incomingRow = Serial.readString();
    if (incomingRow.equals("dumpLogFile")) {
      displayFile(SPIFFS, u8g2, LOGFILE);
    } else if (incomingRow.startsWith("setAlarm")) {
      // Command to set alarms via serial input
      float low, high;
      char type;
      sscanf(incomingRow.c_str(), "setAlarm %c %f %f", &type, &low, &high);
      switch (type) {
        case 'T':
          alarms.temperature_low = low;
          alarms.temperature_high = high;
          prefs.putFloat("temperature_low", low);
          prefs.putFloat("temperature_high", high);
          break;
        case 'H':
          alarms.humidity_low = low;
          alarms.humidity_high = high;
          prefs.putFloat("humidity_low", low);
          prefs.putFloat("humidity_high", high);
          break;
      }
    } else if (incomingRow.equals("formatSPIFFS")) {
      SPIFFS.format();
      Serial.println("SPIFFS formatted.");
    }
  }

  // Handle touch input
  handleTouch();

  // Read temperature and humidity from SHT35
  if (NO_ERROR == sensor.read_meas_data_single_shot(HIGH_REP_WITH_STRCH, &temp, &hum)) {
    updateStats(temp, temp_min, temp_max, temp_sum, temp_count);
    updateStats(hum, humidity_min, humidity_max, humidity_sum, humidity_count);
  } else {
    Serial.println("Failed to read from SHT35 sensor!");
    temp = hum = NAN; // Use NAN to indicate an error
  }

  // Read pressure from DPS310
  if (dps310.measurePressureOnce(pressure, 7) == 0) {
    updateStats(pressure, pressure_min, pressure_max, pressure_sum, pressure_count);
  } else {
    Serial.println("Failed to read from DPS310 sensor!");
    pressure = NAN; // Use NAN to indicate an error
  }

  // Display based on current state
  u8g2.clearBuffer();

  switch (currentState) {
    case CURRENT_VALUES:
      if (!isnan(temp)) u8g2.drawStr(0, 10, ("Temp: " + String(temp, 1) + " C").c_str());
      if (!isnan(hum)) u8g2.drawStr(0, 28, ("Humidity: " + String(hum, 1) + " %").c_str());
      if (!isnan(pressure)) u8g2.drawStr(0, 46, ("Pressure: " + String(pressure / 100.0, 2) + " hPa").c_str());
      break;
    case TEMP_STATS:
      u8g2.drawStr(0, 10, ("Temp: " + String(temp, 1) + " C").c_str());
      u8g2.drawStr(0, 28, ("Min: " + String(temp_min, 1) + " C").c_str());
      u8g2.drawStr(0, 46, ("Max: " + String(temp_max, 1) + " C").c_str());
      u8g2.drawStr(0, 56, ("Avg: " + String(temp_sum / temp_count, 1) + " C").c_str());
      break;
    case HUMIDITY_STATS:
      u8g2.drawStr(0, 10, ("Humidity: " + String(hum, 1) + " %").c_str());
      u8g2.drawStr(0, 28, ("Min: " + String(humidity_min, 1) + " %").c_str());
      u8g2.drawStr(0, 46, ("Max: " + String(humidity_max, 1) + " %").c_str());
      u8g2.drawStr(0, 56, ("Avg: " + String(humidity_sum / humidity_count, 1) + " %").c_str());
      break;
    case PRESSURE_STATS:
      u8g2.drawStr(0, 10, ("Pressure: " + String(pressure / 100.0, 2) + " hPa").c_str());
      u8g2.drawStr(0, 28, ("Min: " + String(pressure_min / 100.0, 2) + " hPa").c_str());
      u8g2.drawStr(0, 46, ("Max: " + String(pressure_max / 100.0, 2) + " hPa").c_str());
      u8g2.drawStr(0, 56, ("Avg: " + String(pressure_sum / pressure_count / 100.0, 2) + " hPa").c_str());
      break;
  }

  u8g2.sendBuffer();

  delay(500); // Short delay to reduce unnecessary CPU usage
}
